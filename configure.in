dnl $Id: configure.in,v 1.26 2003/06/28 17:36:57 tom Exp $
dnl Process this file with autoconf to produce a configure script.
dnl
AC_PREREQ(2.13.20000819)
AC_INIT(dialog.h)
AC_CONFIG_HEADER(config.h:config.hin)

dnl PACKAGE is used for GNU gettext
PACKAGE=dialog
AC_DEFINE_UNQUOTED(PACKAGE, "$PACKAGE")
AC_SUBST(PACKAGE)

DESTDIR=
AC_SUBST(DESTDIR)

dnl
dnl Checks for programs.
dnl
AC_PROG_CC
AC_PROG_CPP
AC_PROG_GCC_TRADITIONAL
AC_PROG_MAKE_SET
AC_PROG_RANLIB
AC_PROG_INSTALL
AC_ISC_POSIX

AC_C_CONST

CF_MAKEFLAGS
CF_MAKE_TAGS
CF_DISABLE_ECHO
CF_PROG_EXT
CF_LIB_PREFIX
CF_GNU_SOURCE

AC_ARG_WITH(warnings,
[  --with-warnings         turn on gcc warnings, for debugging ],[
	CF_GCC_ATTRIBUTES
	CF_GCC_WARNINGS
])

dnl
dnl Checks for libraries.
dnl
CF_BUNDLED_INTL(makefile,enable)
CF_MATH_LIB(,sqrt(x))

CF_WITH_DBMALLOC
CF_WITH_DMALLOC

use_ncurses=no
AC_ARG_WITH(ncurses,
	[  --with-ncurses          compile/link with ncurses library],
	[use_ncurses=ncurses])
AC_ARG_WITH(ncursesw,
	[  --with-ncursesw         compile/link with wide-char ncurses library],
	[use_ncurses=ncursesw])
if test $use_ncurses != no ; then
	if test $use_ncurses = ncursesw ; then
		CF_UTF8_LIB
	fi
	CF_NCURSES_CPPFLAGS($use_ncurses)
	CF_NCURSES_LIBS($use_ncurses)
else
	CF_CURSES_CPPFLAGS
	CF_NCURSES_VERSION
	CF_CURSES_LIBS
fi

EXTRAOBJS=""
cf_all_widgets=yes

CF_ARG_MSG_ENABLE([if you want config-file support],
	rc-file,
	[  --disable-rc-file       do not include config-file support],
	[EXTRAOBJS="$EXTRAOBJS rc.o"
	 AC_DEFINE(HAVE_RC_FILE)],,$cf_all_widgets)

CF_ARG_MSG_ENABLE([if you want Xdialog-style dialogs],
	Xdialog,
	[  --disable-Xdialog       do not include Xdialog-style dialogs],
	[EXTRAOBJS="$EXTRAOBJS calendar.o fselect.o timebox.o"
	 AC_DEFINE(HAVE_XDIALOG)],,$cf_all_widgets)

CF_ARG_MSG_ENABLE([if you want the gauge dialog],
	gauge,
	[  --disable-gauge         do not include the gauge dialog],
	[EXTRAOBJS="$EXTRAOBJS guage.o"
	 AC_DEFINE(HAVE_GAUGE)],,$cf_all_widgets)

CF_ARG_MSG_ENABLE([if you want the tailbox dialog],
	tailbox,
	[  --disable-tailbox       do not include the tailbox dialog],
	[EXTRAOBJS="$EXTRAOBJS tailbox.o"
	 AC_DEFINE(HAVE_TAILBOX)],,$cf_all_widgets)

AC_SUBST(EXTRAOBJS)

dnl
dnl Checks for header files.
dnl
AC_HEADER_STDC
AC_HEADER_TIME
AC_HEADER_DIRENT
AC_CHECK_HEADERS(unistd.h ncurses/term.h)

dnl
dnl Checks for library functions.
dnl
AC_TYPE_SIGNAL
AC_CHECK_FUNCS(\
strcasecmp \
waitpid \
)

CF_CURSES_FUNCS(\
wget_wch \
)

AC_CHECK_FUNC(start_color,[AC_DEFINE(HAVE_COLOR)])
CF_CURSES_CHTYPE
CF_FUNC_WAIT

AC_TRY_LINK([#include <locale.h>],[setlocale(LC_ALL, "")],[AC_DEFINE(HAVE_SETLOCALE)])

AC_OUTPUT(makefile $SUB_MAKEFILE samples/install/makefile,,,sort -u)
